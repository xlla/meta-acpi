/*
 * Intel Edison
 *
 * This adds SSD1331 96x64 display device ,0.95inch, 65535 color OLED 
 *
 * In Linux you need to set CONFIG_FB_TFT_SSD1331=y (or m) to be able to use
 * this device.
 *
 * Copyright (C) 2019, Intel Corporation
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 *
 * Include this file from an ASL providing SSDT DefinitionBlock.
 */

External (\_SB.PCI0.SPI5, DeviceObj)

Scope (\_SB.PCI0.SPI5)
{
    Device (OLE0) {
        Name (_HID, "SSD1331")
        Name (_CID, "PRP0001")
        Name (_DDN, "zjy 65k color OLED")
        Name (_CRS, ResourceTemplate () {
            SpiSerialBusV2 (
                1,                      // Chip select
                PolarityLow,            // Chip select is active low
                FourWireMode,           // Full duplex
                8,                      // Bits per word is 8 (byte)
                ControllerInitiated,    // Don't care
                1000000,               // 1 MHz
                ClockPolarityLow,       // SPI mode 0
                ClockPhaseFirst,        // SPI mode 0
                "\\_SB.PCI0.SPI5",      // SPI host controller
                0                       // Must be 0
            )
            GpioIo(Exclusive, PullDown, 0, 0, IoRestrictionOutputOnly,
                "\\_SB.PCI0.GPIO", 0, ResourceConsumer, , ) { 49 }
            GpioIo(Exclusive, PullUp, 0, 0, IoRestrictionOutputOnly,
                "\\_SB.PCI0.GPIO", 0, ResourceConsumer, , ) { 48 }
        })

        /*
         * See drivers/staging/fbtft/fbtft-core.c::fbtft_properties_read()
         * for more information about these bindings.
         */
        Name (_DSD, Package () {
            ToUUID("daffd814-6eba-4d8c-8a91-bc9bbf4aa301"),
            Package () {
                Package () { "compatible", Package () { "solomon,ssd1331" } },
                Package () { "dc-gpios", Package () { ^OLE0, 0, 0, 0 } },
                Package () { "reset-gpios", Package () { ^OLE0, 1, 0, 1 } },
                Package () { "width", 96 },
                Package () { "height", 64 },
                Package () { "buswidth", 8 },
            }
        })
    }
}
